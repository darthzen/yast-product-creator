/**
 * File:	include/product-creator/kiwi.ycp
 * Package:	Configuration of product-creator
 * Summary:	Dialogs for kiwi configuration
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */
{
  textdomain "product-creator";

  import "CWM";
  import "CWMTab";
  import "Directory";
  import "ProductCreator";
  import "FileUtils";
  import "Label";
  import "Kiwi";
  import "Popup";
  import "SourceDialogs";
  import "Summary";
  import "URL";
  import "Wizard";

  include "product-creator/dialogs.ycp";

  // map of current Live ISO configuration
  map<string,any> KiwiConfig	= $[];

  // what are we configuring now ("iso"/"xen"/...)
  string kiwi_task	= "";

  // return the id of actual widget for boot image
  string boot_image_widget () {
    return kiwi_task + "boot";
  }

  // read available the images under /usr/share/kiwi/image/ directory
  list<term> GetAvailableImages (string subdir) {

    list<term> ret	= [];
    string dir          = "/usr/share/kiwi/image/";
    map out = (map) SCR::Execute (.target.bash_output,
	sformat ("ls %1/%2", dir, subdir));
    if (out["exit"]:0 != 0)
	return ret;
    foreach (string file, splitstring (out["stdout"]:"", "\n"), {
	if (file != "" && FileUtils::IsDirectory (sformat ("%1/%2/%3", dir, subdir, file)))
	{
	    ret	= add (ret, `item (`id(file), file, file == KiwiConfig[subdir]:""));
	}
    });
    return ret;
  }

  /********************************************************************************************
   * widget handlers
   *******************************************************************************************/

  /**
   * Initialize the widget with ignored packages
   */
  define void InitSWIgnore (string id) {
    string sw_selection	= KiwiConfig["sw_selection"]:"image";
    string ignore_key	= sw_selection == "image" ? "ignore" : (sw_selection + "_ignore");
    UI::ChangeWidget (`id (id), `Value, mergestring (KiwiConfig[ignore_key]:[], "\n"));
  }

  define void StoreSWIgnore (string key, map event) {
    string sw_selection	= KiwiConfig["sw_selection"]:"image";
    string ignore_key	= sw_selection == "image" ? "ignore" : (sw_selection + "_ignore");
    KiwiConfig[ignore_key]	= filter (string p,
	splitstring ((string) UI::QueryWidget(`id(key), `Value), "\n"), ``(p != ""));
  }

  define symbol HandleSWIgnore (string key, map event) {
    if (event["ID"]:nil == `next) StoreSWIgnore (key, event);
    return nil;
  }


  /**
   * Initialize the contents of richtext with selected software
   */
  define void InitSWRichText (string id) {

    string sw_selection	= KiwiConfig["sw_selection"]:"image";
    string pattern_key	= sw_selection == "image" ? "addons" : (sw_selection + "_patterns");
    string rt	= "";
    if (KiwiConfig[pattern_key]:[] != [])
    {
	// richtext header
	rt	= Summary::AddHeader ("", _("Patterns"));
	foreach (string pattern, (list<string>) KiwiConfig[pattern_key]:[], {
	    rt	= Summary::AddListItem (rt, pattern);
	});
    }
    string package_key	= sw_selection == "image" ? "packages" : (sw_selection + "_packages");
    if (KiwiConfig[package_key]:[] != [])
    {
	// richtext header
	rt	= Summary::AddHeader (rt, _("Packages"));
	foreach (string package, (list<string>) KiwiConfig[package_key]:[], {
	    rt	= Summary::AddListItem (rt, package);
	});
    }
    UI::ChangeWidget (`id (id), `Value, rt);
  }

  /**
   * Handler for software selection
   */
  define symbol HandleSWSelection (string key, map event) {
    if (event["ID"]:nil == key)
    {
	string sw_selection	= KiwiConfig["sw_selection"]:"image";

	integer mbytes  = (integer) UI::QueryWidget (`id ("size"), `Value);
        if (UI::QueryWidget (`id ("sizeunit"), `Value) == "G")
	    mbytes      = mbytes * 1024;
	ProductCreator::max_size_mb     = mbytes;
	if (sw_selection == "image" && packageSelector () != `cancel)
	{
	    KiwiConfig["addons"]	= ProductCreator::Config["addons"]:[];
	    KiwiConfig["packages"]	= ProductCreator::Config["packages"]:[];
	    InitSWRichText ("rt_sw");
	    if (ProductCreator::Config["taboo"]:[] != nil)
	    {
		//FIXME do not replace current 'ignored' tags...
		KiwiConfig["ignore"]	= union (KiwiConfig["ignore"]:[],
		    ProductCreator::Config["taboo"]:[]);
		InitSWIgnore ("ignore");
	    }
	}
	else if (sw_selection != "image")
	{
	    map<string,any> selected = runPackageSelector ("",
		KiwiConfig[sw_selection + "_patterns"]:[],
		KiwiConfig[sw_selection + "_packages"]:[],
		KiwiConfig[sw_selection + "_ignore"]:[],
		`packages
	    );
	    KiwiConfig[sw_selection + "_patterns"]	= selected["addons"]:[];
	    KiwiConfig[sw_selection + "_packages"]	= selected["packages"]:[];
	    InitSWRichText ("rt_sw");
	    if (ProductCreator::Config["taboo"]:[] != nil)
	    {
		KiwiConfig["ignore"]	= ProductCreator::Config["taboo"]:[];
		InitSWIgnore ("ignore");
	    }
	}
    }
    return nil;
  }

  /**
   * initialize the combo box with package selection groups
   */
  define void InitSWSelectionCombo (string id) {
    string sw_selection	= KiwiConfig["sw_selection"]:"image";
    list items	= [
	`item (`id ("image"), _("Image"), sw_selection == "image"),
    ];
    if (kiwi_task == "xen")
	items	= add (items, `item (`id ("xen"), _("Xen Specific"), sw_selection == "xen"));
    UI::ChangeWidget (`id (id), `Items, items);
  }

  define void StoreSWSelectionCombo (string key, map event) {
      KiwiConfig[key]	= (string) UI::QueryWidget(`id(key), `Value);
  }

  /**
   * handler for combo box with isoboot items
   */
  define symbol HandleSWSelectionCombo (string key, map event) {
    any id	= event["ID"]:nil;
    // store the value on exiting
    if (id == `next) StoreSWSelectionCombo (key, event);
    else if (event["ID"]:nil == key)
    {
	string selected	= (string) UI::QueryWidget(`id(key), `Value);
	if (selected != KiwiConfig["sw_selection"]:"image")
	{
	    StoreSWIgnore ("ignore", event);
	    StoreSWSelectionCombo (key, event);
	    InitSWRichText ("rt_sw");
	    InitSWIgnore ("ignore");
	}
    }
    return nil;
  }


  /**
   * initialize the value of combo box with isoboot items
   */
  define void InitBootCombo (string id) {
    UI::ChangeWidget (`id (id), `Items, GetAvailableImages (id));
  }

  /**
   * store the value of current isoboot image
   */
  define void StoreBootCombo (string key, map event) {
      KiwiConfig[key]	= (string) UI::QueryWidget(`id(key), `Value);
  }
  /**
   * handler for combo box with isoboot items
   */
  define symbol HandleBootCombo (string key, map event) {
    // store the value on exiting
    if (event["ID"]:nil == `next) StoreBootCombo (key, event);
    return nil;
  }

  /**
   * initialize the value of compress checkbox
   */
  define void InitCompressCheckBox (string id) {
    UI::ChangeWidget (`id (id), `Enabled, false);// FIXME squashfs not available?
    UI::ChangeWidget (`id (id), `Value, KiwiConfig["compressed"]:false);
  }

  /**
   * store the value of compress checkbox
   */
  define void StoreCompressCheckBox (string key, map event) {
      KiwiConfig["compressed"]	= (boolean) UI::QueryWidget(`id(key), `Value);
  }
  /**
   * handler for compress checkbox
   */
  define symbol HandleCompressCheckBox (string key, map event) {
    // store the value on exiting
    if (event["ID"]:nil == `next) StoreCompressCheckBox (key, event);
    return nil;
  }

  /**
   * initialize the value of version
   */
  define void InitVersion (string id) {
    UI::ChangeWidget (`id (id), `Value, sformat ("%1", KiwiConfig[id]:"1"));
  }

  /**
   * store the value of current version
   */
  define void StoreVersion (string key, map event) {
      KiwiConfig[key]	= sformat ("%1", UI::QueryWidget(`id(key), `Value));
  }

  /**
   * handler for version
   */
  define symbol HandleVersion (string key, map event) {
    // store the value on exiting
    if (event["ID"]:nil == `next) StoreVersion (key, event);
    return nil;
  }

  /**
   * initialize the value of combo box with isoboot items
   */
  define void InitSizeUnitCombo (string id) {
    UI::ChangeWidget (`id (id), `Items, maplist (string u, [ "M", "G" ], ``(
	`item (`id (u), u + "B", KiwiConfig[id]:"M" == u))
    ));
  }

  /**
   * initialize the value of version
   */
  define void InitSize (string id) {
    UI::ChangeWidget (`id (id), `Value, KiwiConfig[id]:2700);
  }

  /**
   * initialize the product label
   */
  define void InitLabel (string id) {
    UI::ChangeWidget (`id ("name"), `Value, KiwiConfig["name"]:"");
  }


  /**
   * universal widget: initialize the string value of widget @param
   */
  define void InitDescription (string id) {
    UI::ChangeWidget (`id (id), `Value, KiwiConfig[id]:"");
  }

  /**
   * store the string value of given widget
   */
  define void StoreDescription (string key, map event) {
      KiwiConfig[key]	= UI::QueryWidget (`id(key), `Value);
  }

  /**
   * handler for general string-value widgets: store their value on exit/save
   */
  define symbol HandleDescription (string key, map event) {
    // store the value on exiting
    if (event["ID"]:nil == `next) StoreDescription (key, event);
    return nil;
  }

  define void InitOutputDir (string id) {
    UI::ChangeWidget (`id (id), `Value, KiwiConfig["iso-directory"]:"");
  }

  /**
   * initialize the table with users
   */
  define void InitUsersTable (string id) {

    list items = maplist (string user, map usermap, (map<string,map>) KiwiConfig["users"]:$[],
    ``(`item (`id (user), user, usermap["home"]:"")));
    UI::ChangeWidget (`id ("table"), `Items, items);
    UI::ChangeWidget (`id ("edituser"), `Enabled, size (items) > 0);
    UI::ChangeWidget (`id ("deleteuser"), `Enabled, size (items) > 0);
  }

  /**
   * Handle changes in users table
   */
  define symbol HandleAddEditUser (string key, map event) {
    if (event["ID"]:nil != key && (key != "userstable" || event["ID"]:nil != "table"))	return nil;

    if (key == "userstable") key = "edituser";
    string current_user = (string) UI::QueryWidget (`id ("table"), `CurrentItem);

    UI::OpenDialog (`opt(`decorated), `HBox (`HSpacing (0.5), `VBox (
	`VSpacing (0.5),
	// popup label
	`Label (`id (`label), _("Add new user")),
	// text entry label
	`TextEntry (`id (`username), _("&Name")),
	`Password (`id (`pw1), Label::Password(), ""),
	`Password (`id(`pw2), Label::ConfirmPassword(), ""),
	`TextEntry (`id (`home), _("&Home Directory")),
	`HBox (
	    `PushButton (`id(`ok),`opt(`key_F10), Label::OKButton()),
	    `PushButton (`id(`cancel),`opt(`key_F9), Label::CancelButton())
	),
	`VSpacing (0.5)), `HSpacing (0.5))
    );
    if (key == "edituser")
    {
	UI::ChangeWidget (`id (`username), `Value, current_user);
	UI::ChangeWidget (`id (`home), `Value, KiwiConfig["users", current_user, "home"]:"");
	UI::ChangeWidget (`id (`pw1), `Value, "*****");
	UI::ChangeWidget (`id (`pw2), `Value, "*****");
	// popup label
	UI::ChangeWidget (`id (`label), `Value, _("Edit User"));
    }
    any ret	= nil;
    repeat
    {
	ret	= UI::UserInput ();
	if (ret == `ok)
	{
	    string username	= (string) UI::QueryWidget (`id(`username), `Value);
	    string pwd		= (string) UI::QueryWidget (`id(`pw1), `Value);
	    if (username == "")
	    {
		// popup message
		Report::Error (_("Enter the user name."));
		ret     = `notnext;
		continue;
	    }
	    if (pwd != UI::QueryWidget (`id(`pw2), `Value))
	    {
		// popup message
		Report::Error (_("The passwords do not match.
Try again."));
		ret     = `notnext;
		continue;
	    }
	    if (pwd == "")
	    {
		// popup message
		Report::Error (_("Enter the user password."));
		ret     = `notnext;
		continue;
	    }
	    if (! haskey (KiwiConfig, "users"))
		KiwiConfig["users"]	= $[];
	    else if (key == "edituser" && username != current_user)
		KiwiConfig["users"]	= remove (KiwiConfig["users"]:$[], current_user);
	    KiwiConfig["users", username]	= $[
		"pwd"	: pwd,
		"home"	: UI::QueryWidget (`id(`home), `Value)
	    ];
	}
    } until (ret == `ok || ret == `cancel);

    UI::CloseDialog ();
    if (ret == `ok) InitUsersTable ("table");
    return nil;
  }

  /**
   * handle delete user button
   */
  define symbol HandleDeleteUser (string key, map event) {
    if (event["ID"]:nil != key)	return nil;

    string current_user = (string) UI::QueryWidget (`id ("table"), `CurrentItem);
    KiwiConfig["users"]	= remove (KiwiConfig["users"]:$[], current_user);
    InitUsersTable ("table");
    return nil;
  }

  /**
   * initialize the table with root dir contents
   */
  define void InitRootDirTable (string id) {

    list items = maplist (string file, (list<string>) KiwiConfig["root_dir"]:[], {
//	return `item (`id (file), substring (file, findlastof (file, "/") + 1));
	return `item (`id (file), file);
    });
    UI::ChangeWidget (`id ("roottable"), `Items, items);
    UI::ChangeWidget (`id ("root_dir_delete"), `Enabled, size (items) > 0);
  }

  // add new subdir to the 'root' directory
  define symbol HandleAddToRootDir (string key, map event) {

    if (event["ID"]:nil != key)	return nil;
    // popup for file selection dialog
    string dir	= UI::AskForExistingDirectory ("", _("Directory to Import"));
    if (dir!= nil)
    {
	KiwiConfig["root_dir"]	= union (KiwiConfig["root_dir"]:[], [dir]);
	InitRootDirTable ("roottable");
    }
    return nil;
  }

  // delete subdir from the 'root' directory
  define symbol HandleDeleteFromRootDir (string key, map event) {
    if (event["ID"]:nil != key)	return nil;
    string current	= (string) UI::QueryWidget (`id ("roottable"), `Value);
    KiwiConfig["root_dir"]	= filter (string f, KiwiConfig["root_dir"]:[], ``(f != current));
    InitRootDirTable ("roottable");
    return nil;
  }

  /**
   * initialize the table with config dir contents
   */
  define void InitConfigDirTable (string id) {

    list items = maplist (string file, (list<string>) KiwiConfig["config_dir"]:[], {
//	return `item (`id (file), substring (file, findlastof (file, "/") + 1));
	return `item (`id (file), file);
    });
    UI::ChangeWidget (`id ("configtable"), `Items, items);
    UI::ChangeWidget (`id ("config_dir_delete"), `Enabled, size (items) > 0);
  }

  // add new subdir to the 'config' directory
  define symbol HandleAddToConfigDir (string key, map event) {

    if (event["ID"]:nil != key)	return nil;
    // popup for file selection dialog
    string dir	= UI::AskForExistingFile ("", "", _("Script to Import"));
    if (dir!= nil)
    {
	KiwiConfig["config_dir"]	= union (KiwiConfig["config_dir"]:[], [dir]);
	InitConfigDirTable ("configtable");
    }
    return nil;
  }

  // delete subdir from the 'config' directory
  define symbol HandleDeleteFromConfigDir (string key, map event) {
    if (event["ID"]:nil != key)	return nil;
    string current	= (string) UI::QueryWidget (`id ("configtable"), `Value);
    KiwiConfig["config_dir"]	= filter (string f, KiwiConfig["config_dir"]:[], ``(f != current));
    InitConfigDirTable ("configtable");
    return nil;
  }

  /**
   * universal handler for directory browsing
   */
  string BrowseDirectoryHandler (string key, string label) {

    string current	= (string) UI::QueryWidget (`id (key), `Value);
    if (current == nil) current = "";
    string dir	= UI::AskForExistingDirectory (current, label);
    if (dir!= nil)
    {
	UI::ChangeWidget (`id (key), `Value, dir);
	StoreDescription (key, $[]);
    }
    return dir;
  }

  /**
   * handler for 'root' directory browse
   */
  symbol HandleBrowseRootDirectory (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog ('root' is a name, do not translate)
    BrowseDirectoryHandler ("root_dir", _("Path to root Directory"));
    return nil;
  }

  /**
   * handler for 'config' directory browse
   */
  symbol HandleBrowseConfigDirectory (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog ('config' is a name, do not translate)
    BrowseDirectoryHandler ("config_dir", _("Path to config Directory"));
    return nil;
  }

  /**
   * handler for output directory browse
   */
  symbol HandleBrowseOutputDirectory (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog
    BrowseDirectoryHandler ("output_dir", _("Path to the Output Directory"));
    return nil;
  }

  /**
   * universal handler for file browsing
   */
  string BrowseFileHandler (string key, string label) {

    string current	= (string) UI::QueryWidget (`id (key), `Value);
    if (current == nil) current = "";
    string file = UI::AskForExistingFile (current, "", label);
    if (file != nil && key != "config.sh")
    {
	UI::ChangeWidget (`id (key), `Value, file);
	StoreDescription (key, $[]);
    }
    return file;
  }

  /**
   * Handler for browsing for images.sh file
   */
  symbol HandleBrowseImagesFile (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog
    BrowseFileHandler ("images_path", _("Path to images.sh File"));
    return nil;
  }

  /**
   * Handler for browsing for autoyast profile
   */
  symbol HandleBrowseAutoYaSTFile (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog
    BrowseFileHandler ("autoyast_path", _("Path to AutoYaST Profile"));
    return nil;
  }

  /**
   * Handler for importing config.sh file
   */
  symbol HandleImportConfigFile (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog
    string file	= BrowseFileHandler ("config.sh", _("Path to config.sh File"));
    if (file != nil)
    {
	if (FileUtils::Exists (file))
	{
	    string configsh	= (string) SCR::Read (.target.string, file);
	    if (configsh != nil)
	    {
		UI::ChangeWidget (`id ("config.sh"), `Value, configsh);
		StoreDescription ("config.sh", event);
	    }
	}
    }
    return nil;
  }

  /**
   * Handler for importing images.sh file
   * TODO same function as HandleImportConfigFile
   */
  symbol HandleImportImagesFile (string key, map event) {
    if (event["ID"]:nil != key) return nil;
    // popup for file selection dialog
    string file	= BrowseFileHandler ("images.sh", _("Path to images.sh File"));
    if (file != nil)
    {
	if (FileUtils::Exists (file))
	{
	    string imagessh	= (string) SCR::Read (.target.string, file);
	    if (imagessh != nil)
	    {
		UI::ChangeWidget (`id ("images.sh"), `Value, imagessh);
		StoreDescription ("images.sh", event);
	    }
	}
    }
    return nil;
  }

  // generic popup
  define symbol NotImplementedHandler (string key, map event) {
    if (event["ID"]:nil == key)
    {
	Popup::Message (_("Feature not implemented yet."));
    }
    return nil;
  }

  /**
   * handler for main action: create the iso image with kiwi
   */
  define boolean CreateImage (string key, map event) {
    if (event["ID"]:nil != `next)
	return true;
    boolean ret	= true;
    map question	= $[
	// popup question
	"iso"	: _("Create ISO image now?"),
	// popup question
	"xen"	: _("Create Xen image now?"),
	// popup question
	"usb"	: _("Create USB stick image now?"),
	// popup question
	"vmx"	: _("Create virtual disk image now?"),
    ];

    map success		= $[
	// popup message, %1 is a dir
	"iso"	: _("ISO image successfully created in
%1
directory."),
	// popup message, %1 is a dir
	"xen"	: _("Xen image files successfully created in
%1
directory.
"),
	// popup message, %1 is a dir
	"usb"	: _("USB stick image successfully created in
%1
directory."),
	// popup message, %1 is a dir
	"vmx"	: _("Virtual disk image successfully created in
%1
directory."),
    ];

    Kiwi::WriteConfigXML (KiwiConfig, kiwi_task);
    if (Popup::YesNo (question[kiwi_task]:""))
    {
	if (true)
	{
	    string out_dir	= KiwiConfig["iso-directory"]:"";
	    if (FileUtils::CheckAndCreatePath (out_dir) && Kiwi::PrepareAndCreate (out_dir))
	    {
		if (kiwi_task == "usb" && false)
		{
		    // additional question (yes/no)
		    if (Popup::YesNo (sformat (success[kiwi_task]:"", out_dir) + _("
Save the image to the stick now?")))
		    {
			NotImplementedHandler ("", $[ "ID" : ""]);
		    }
		}
		else
		{
		    Popup::Message (sformat (success[kiwi_task]:"", out_dir));
		}
	    }
	    else
	    {
		ret	= false;
	    }
	}
    }
    string dir	= Kiwi::SaveConfiguration (KiwiConfig, kiwi_task);
    if (dir != nil && dir != "")
    {
	ProductCreator::Config["kiwi_configuration_" + kiwi_task] = dir;
    }
    return ret;
  }

  /**
   * Global init function for Kiwi image dialog
   * - read saved settings and fill in defaults
   */
  define void InitImageConfiguration () {

    kiwi_task	= ProductCreator::kiwi_task;
    string kiwi_dir     = "/usr/share/kiwi/image/";

    // read the information from the base product
    integer src_id	= ProductCreator::checkProductDependency ();
    map<string,string> content = ProductCreator::ReadContentFile(src_id);

    KiwiConfig	= ProductCreator::Config;
    // busy popup
    Popup::ShowFeedback (_("Reading current live image configuration..."), _("Please wait..."));

    map default_dirmap	= $[
	"iso"	: sformat ("%1/kwliveDVD-suse-10.3", kiwi_dir),
	"xen"	: sformat ("%1/buildhost-suse-SLES10", kiwi_dir),
	"usb"	: sformat ("%1/kwliveDVD-suse-10.3", kiwi_dir),
	"vmx"	: sformat ("%1/kwliveDVD-suse-10.3", kiwi_dir),
    ];

    // path to current config directory
    string default_dir	= default_dirmap[kiwi_task]:"";
    string kiwi_configuration = KiwiConfig["kiwi_configuration_" + kiwi_task]:default_dir;

    if (!FileUtils::Exists (kiwi_configuration))
    {
	// use local template if default dir does not exist (bug #289552)
	if (!FileUtils::Exists (default_dir))
	{
	    default_dir = Directory::datadir+"/product-creator/kiwi_templates/";
	    default_dir	= default_dir + (kiwi_task == "xen" ? "xen" : "iso");
	}
	y2warning ("directory %1 is not available, using %2", kiwi_configuration, default_dir);
	kiwi_configuration	= default_dir;
    }

    if (FileUtils::Exists (kiwi_configuration + "/root"))
    {
	// read all entries from root_dir and save to list
	map out = (map) SCR::Execute (.target.bash_output,
	    sformat ("ls -A1 -d %1/root/* 2>/dev/null", kiwi_configuration));
	KiwiConfig["root_dir"]	= filter (string f, splitstring(out["stdout"]:"", "\n"), ``(f!=""));
    }
    if (FileUtils::Exists (kiwi_configuration + "/config"))
    {
	map out = (map) SCR::Execute (.target.bash_output,
	    sformat ("ls -A1 -d %1/config/* 2>/dev/null", kiwi_configuration));
	KiwiConfig["config_dir"] = filter (string f, splitstring(out["stdout"]:"", "\n"),``(f!=""));
    }

    foreach (string file, [ "images.sh", "config.sh" ], {
	string file_path	= kiwi_configuration + "/" + file;
	if (FileUtils::Exists (file_path))
	{
	    string contents	= (string) SCR::Read (.target.string, file_path);
	    if (contents != nil)
		KiwiConfig[file]	= contents;
	}
    });
    // take care of the rest in the input directory (#330052)
    list import_files   = [];
    map out = (map) SCR::Execute (.target.bash_output,
	sformat ("ls -A1 -d %1/* 2>/dev/null", kiwi_configuration));
    foreach (string line, splitstring (out["stdout"]:"", "\n"), {
	if (line == "") return;
	string last     = substring (line, findlastof (line, "/") + 1);
	if (contains (["images.sh", "config.sh", "config.xml", "root","config"],
	    last))
	    return;
	import_files    = add (import_files, line);
    });
    KiwiConfig["import_files"]  = import_files;

    map<string,any> read_config = Kiwi::ReadConfigXML (kiwi_configuration);
    if (!KiwiConfig["_imported"]:false)
    {
	KiwiConfig = (map<string,any>) union (KiwiConfig, read_config);
	// for new configuration, do not read all from the template
	y2milestone ("product-creator based configuration...");
	foreach (string key, ["addons", "packages", "sources"], {
	    KiwiConfig[key]	= ProductCreator::Config[key]:[];
	});
    }
    else
    {
	y2milestone ("imported configuration...");
	// ignore options already set earlier...
	foreach (string key, any val, read_config, {
	    if (!contains (["name", "iso-directory", "sources"], key))
	    {
		KiwiConfig[key] = val;
	    }
	});
	// "sources" are already there...
	foreach (string key, ["addons", "packages"], {
	    ProductCreator::Config[key]	= KiwiConfig[key]:[];
	});
    }

    map label2boot	= $[
	    "openSUSE 10.2"	: "suse-10.2",
	    "openSUSE 10.3"	: "suse-10.3",
	    "SUSE Linux 10.1"	: "suse-10.1",
	    "SUSE Linux Enterprise Server 10"	: "suse-SLES10",
	    "SUSE Linux Enterprise Desktop 10"	: "suse-SLED10"
    ];
    map distproduct2boot	= $[
	"openSUSE-10.2-CD-download"		: "suse-10.2",
	"openSUSE-10.2-dvd5-download"		: "suse-10.2",
	"openSUSE-10.3-dvd5-download"		: "suse-10.3",
	"openSUSE-10.3-CD-download"		: "suse-10.3",
	"SUSE-Linux-10.1-CD-download-x86"	: "suse-10.1",
	"SUSE-Linux-Enterprise-Server-i386"	: "suse-SLES10",
	"SUSE-Linux-Enterprise-Desktop-i386"	: "suse-SLED10",
	"SUSE-Linux-Enterprise-Server-SP1"	: "suse-SLES10-SP1",
	"SUSE-Linux-Enterprise-Desktop-SP1"	: "suse-SLED10-SP1"
    ];
    string boot_image	= label2boot[content["LABEL"]:""]:"";
    if (boot_image == "")
	boot_image	= distproduct2boot[content["DISTPRODUCT"]:""]:"";

    // guess some default boot image value
    if (kiwi_task == "iso" && !haskey (KiwiConfig, "isoboot") && boot_image != "")
    {
	KiwiConfig["isoboot"]	= boot_image;
    }
    else if (kiwi_task == "usb")
    {
	if (!haskey (KiwiConfig, "usbboot") && boot_image != "")
	    KiwiConfig["usbboot"]	= boot_image;
    }
    else if (kiwi_task == "vmx")
    {
	if (!haskey (KiwiConfig, "vmxboot") && boot_image != "")
	    KiwiConfig["vmxboot"]	= boot_image;
    }
    else if (kiwi_task == "xen")
    {
	if (!haskey (KiwiConfig, "xenboot"))
	    KiwiConfig["xenboot"]	= boot_image;
    }
    string name		= KiwiConfig["name"]:"";
    if (!haskey (KiwiConfig, "specification"))
	KiwiConfig["specification"]	= KiwiConfig["name"]:"";
    Popup::ClearFeedback ();
  }


  /********************************************************************************************
   * widget descriptions
   *******************************************************************************************/

  // return map with description of tabs
  // it is a function, to be able to adapt to actual state (e.g. boot_image_widget ())
  map tabs_descr () {

    return $[
    "config.xml" : $[
	// tab header
	"header"	: _("Image Configuration"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		/*
		`HBox (
		    `HWeight (1, `HBox (
			boot_image_widget ()
//			`VBox (`Label (""), "configure_boot")
		    )),
		    `HWeight (1, `VBox (`Label (""), `Left ("compressed")))
		),
		*/
		`VSpacing (0.2),
		`HBox (
		    `HWeight (1, "version"),
		    `HWeight (1, `HBox ("size", "sizeunit"))
		),
		"sw_selection",
		`HBox (
		    `HWeight (1, `VBox (
			`Left (`Label (_("Installed Software"))), "rt_sw", `Right ("configure_sw")
		    )),
		    `HWeight (1, "ignore")
		),
		`VSpacing (0.2)
	    ), `HSpacing(1)),
	"widget_names" : [
	    "version",
	    "size", "sizeunit",
	    "sw_selection",
	    "rt_sw", "configure_sw",
	    "ignore",
	],
    ],
    "description" : $[
	// tab header
	"header"	: _("Description"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		"author",
		`VSpacing (0.2),
		"contact",
		`VSpacing (0.2),
		"specification",
		`VStretch ()
	    ), `HSpacing (1)),
	"widget_names" : [ "author", "contact", "specification" ],
    ],
    "users" : $[
	// tab header
	"header"	: _("Users"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		"general_users",
		`VSpacing (0.2),
		"group",
		`VSpacing (0.2),
		"userstable",
		`VSpacing (0.2),
		`HBox ("adduser", "edituser", "deleteuser", `HStretch ()),
		`VSpacing (0.2)
	    ), `HSpacing (1)),
	"widget_names" : [
	    "general_users", "group", "userstable", "adduser", "edituser", "deleteuser"
	]
    ],
    "scripts" : $[
	// tab header
	"header"	: _("Scripts"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		"general_scripts",
		`VSpacing (0.2),
		`HBox ("config.sh", `Bottom ("import_config.sh")),
		`HBox ("images.sh", `Bottom ("import_images.sh")),
		`VSpacing (0.2)
	    ), `HSpacing (1)),
	"widget_names" : [
	    "general_scripts",
	    "config.sh", "import_config.sh",
	    "images.sh", "import_images.sh",
	],
    ],
    "directories" : $[
	// tab header
	"header"	: _("Directories"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		"general_directories",
		`VSpacing (0.2),
		"root_dir_table",
		`VSpacing (0.2),
		`HBox ("root_dir_add", `Left ("root_dir_delete")),
		`VSpacing (0.2),
		"config_dir_table",
		`VSpacing (0.2),
		`HBox ("config_dir_add", `Left ("config_dir_delete")),
		`VSpacing (0.2),
		`VSpacing (0.2)
	    ), `HSpacing (1)),
	"widget_names" : [
	    "general_directories",
	    "root_dir_table", "root_dir_add", "root_dir_delete",
	    "config_dir_table", "config_dir_add", "config_dir_delete",
	],
    ],
    ];
  }

  map<string, map> get_widget_description () {
    return $[
    // global widgets
    "global"		:$[
	"widget"	: `empty,
	"validate_type"	: `function,
	"validate_function"	: CreateImage,
	"no_help"	: true,
    ],
    "configure_boot"	: $[
	"widget"	: `push_button,
	// pusbutton label
	"label"		: _("C&onfigure..."),
	"help"		: _("<p>Create a new boot image with <b>Configure</b>.</p>"),
	"handle"        : NotImplementedHandler
     ],
    "compressed" : $[
	"widget"	: `checkbox,
	// textentry label
	"label"		: _("Co&mpress"),
	"help"		: _("<p>Check <b>Compress</b> if the final image should be based \non the squashfs compressed file system."),
	"init"		: InitCompressCheckBox,
	"store"		: StoreCompressCheckBox,
	"handle"	: HandleCompressCheckBox,
    ],
    "sw_selection"	: kiwi_task == "xen" ? $[
	"widget"	: `combobox,
	"opt"		: [ `hstretch, `notify ],
	// combo box label
	"label"		: _("So&ftware Selection"),
	// help text for "So&ftware selection"
	"help"		: "",
	"items"		: [],
	"init"		: InitSWSelectionCombo,
	"store"		: StoreSWSelectionCombo,
	"handle"	: HandleSWSelectionCombo,
    ] : $[
	"widget"	: `empty,
	"no_help"	: true,
    ],
    "rt_sw"		: $[
	"widget"	: `richtext,
	"init"		: InitSWRichText,
	"help"		: "",
	"label"		: "&L",
    ],
    "configure_sw"	: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"         : _("Ch&ange..."),
	"help"		: _("<p>Adapt the software selection with <b>Change</b>.</p>"),
	"handle"	: HandleSWSelection
    ],
    "ignore"	: $[
	"widget"	: `multi_line_edit,
	// label
	"label"		: _("&Ignored Software"),
	"init"		: InitSWIgnore,
	"store"		: StoreSWIgnore,
	"handle"	: HandleSWIgnore,
	// help text for "&Ignored software"
	"help"		: _("<p>For <b>ignored software</b>, enter each entry (like 'smtp_daemon') on new line</p>"),
    ],
    "output_dir" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("O&utput Directory"),
	"help"		: _("help for output dir"),
	"init"		: InitOutputDir,
    ],
    "browse_output_dir"	: $[
	"widget"	: `push_button,
	"label"		: Label::BrowseButton (),
	"help"		: "",
	"handle"	: HandleBrowseOutputDirectory
    ],
    "version" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Version"),
	"help"		: _("<p>Enter the <b>Version</b> of your image configuration.</p>"),
	"valid_chars"	: String::CDigit () + ".",
	"init"		: InitVersion,
	"store"		: StoreVersion,
	"handle"	: HandleVersion,
    ],
    "size" : $[
	"widget"	: `intfield,
	"opt"		: [ `hstretch ],
	// textentry label
	"label"		: _("&Size"),
	"help"		: _("<p>Set the image <b>Size</b> in the specified <b>Unit</b>.</p>"),
	"init"		: InitSize,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "sizeunit" : $[
	"widget"	: `combobox,
	// textentry label
	"label"		: _("&Unit"),
	"no_help"	: true,
	"items"		: [],
	"init"		: InitSizeUnitCombo,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    // ---------------- widgtes for directory structure
    "general_scripts"	: $[
	"widget"	: `empty,
	// general help for directory structure tab
	"help"		: _("<p>Here, edit the configuration scripts used to build your image.</p>"),
    ],
    "general_directories"	: $[
	"widget"	: `empty,
	// general help for directory structure tab
	"help"		: _("<p>Here, point to the configuration directories that should help to build your image.</p>"),
    ],
    "root_dir"		: $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Directory with System Configur&ation"),
	// help text
	"help"		: _("<p>Define the path to the <b>Directory with System Configuration</b> (the <tt>root</tt> directory). The entire directory is copied into the root of the image tree using <tt>cp -a</tt>.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "browse_root_dir"	: $[
	"widget"	: `push_button,
	"label"		: Label::BrowseButton (),
	"help"		: "",
	"handle"	: HandleBrowseRootDirectory
    ],
    "root_dir_table" : $[
	"widget"	: `custom,
	"custom_widget" : `VBox (
	    // label (above table)
	    `Left (`Label (_("Directory with System Configuration"))),
	    `Table (`id("roottable"), `header (
		// table header
		_("Path to Directory"))
	    )
	),
	// help for table with users
	"help"		: _("<p>Configure the <b>Directory with System Configuration</b> (the <tt>root</tt> directory). The entire directory is copied into the root of the image tree using <tt>cp -a</tt>.</p>"),
	"init"		: InitRootDirTable,
    ],
    "root_dir_add"	: $[
	"widget"	: `push_button,
	"label"		: Label::AddButton (),
	"no_help"	: true,
	"handle"	: HandleAddToRootDir,
    ],
    "root_dir_delete"	: $[
	"widget"	: `push_button,
	"label"		: Label::DeleteButton (),
	"no_help"	: true,
	"handle"	: HandleDeleteFromRootDir,
    ],
    "config_dir_table" : $[
	"widget"	: `custom,
	"custom_widget" : `VBox (
	    // label (above table)
	    `Left (`Label (_("Directory with Scripts"))),
	    `Table (`id("configtable"), `header (
		// table header
		_("Path to File"))
	    )
	),
	// help for table with users
	"help"		: _("<p>Configure the <b>Directory with Scripts</b> (the <tt>config</tt> directory). It contains scripts that are run after the installation of all the image packages.</p>"),
	"init"		: InitConfigDirTable,
    ],
    "config_dir_add"	: $[
	"widget"	: `push_button,
	"label"		: Label::AddButton (),
	"no_help"	: true,
	"handle"	: HandleAddToConfigDir,
    ],
    "config_dir_delete"	: $[
	"widget"	: `push_button,
	"label"		: Label::DeleteButton (),
	"no_help"	: true,
	"handle"	: HandleDeleteFromConfigDir,
    ],
    "config.sh" : $[
	"widget"	: `multi_line_edit,
	// textentry label
	"label"		: _("I&mage Configuration Script"),
	"help"		: _("<p>Edit your <b>Image Configuration Script</b>, called <tt>config.sh</tt>. This script is run at the end of the installation but before the package scripts have run.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "import_config.sh" : $[
	"widget"	: `push_button,
	// textentry label
	"label"		: _("&Import..."),
	"help"		: "",
	"handle"	: HandleImportConfigFile
    ],
    "config_dir" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Pa&th to Directory with Scripts"),
	"help"		: _("<p>The optional <b>Directory with Scripts</b> (<tt>config</tt> directory) contains scripts that are run after the installation of all the image packages.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "browse_config_dir"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("Br&owse..."),
	"help"		: "",
	"handle"	: HandleBrowseConfigDirectory
    ],
    "autoyast_path" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Path to Auto&YaST Profile"),
	"help"		: _("Choose the path to the AutoYaST profile (config.xml)."),
    ],
    "browse_autoyast"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("Brow&se..."),
	"help"		: "",
	"handle"	: HandleBrowseAutoYaSTFile
    ],
    "images.sh" : $[
	"widget"	: `multi_line_edit,
	// textentry label
	"label"		: _("C&leanup Script"),
	"help"		: _("<p>Edit your <b>Cleanup Script</b> (<tt>images.sh</tt>). This script is run at the beginning of the image creation process.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "import_images.sh" : $[
	"widget"	: `push_button,
	// textentry label
	"label"		: _("Im&port..."),
	"help"		: "",
	"handle"	: HandleImportImagesFile
    ],
    // ---------------- widgtes for description tab
    "author" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Author"),
	// help text for Author, Contact and Specification widgets
	"help"		: _("<p>Set the values for <b>Author</b> of the image, <b>Contact Information</b>, and the image <b>Specification</b>.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "contact" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("C&ontact"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
	"no_help"	: true,
    ],
    "specification" : $[
	"widget"	: `multi_line_edit,
	// textentry label
	"label"		: _("&Specification"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
	"no_help"	: true,
    ],
    // ---------------- widgtes for users tab
    "general_users"	: $[
	"widget"	: `empty,
	// general help for users tab
	"help"		: _("<p>Here, create users that should be available in the target system.</p>"),
    ],
    "group" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Group name"),
	"help"		: _("<p>Select the <b>Group Name</b> to which the users belong.</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "userstable" : $[
	"widget"	: `custom,
	"custom_widget" : `Table (`id("table"), `opt (`notify), `header(
			    // table header
			    _("Name"),
			    // table header
			    _("Home Directory"))),
	// help for table with users
	"help"		: _("<p>For each user, specify <b>Name</b>, <b>Password</b>, and <b>Home Directory</b>.</p>"),
	"init"		: InitUsersTable,
	"handle"	: HandleAddEditUser,
    ],
    "adduser"	: $[
	"widget"	: `push_button,
	"label"		: Label::AddButton (),
	"no_help"	: true,
	"handle"	: HandleAddEditUser,
    ],
    "edituser"	: $[
	"widget"	: `push_button,
	"label"		: Label::EditButton (),
	"no_help"	: true,
	"handle"	: HandleAddEditUser,
    ],
    "deleteuser"	: $[
	"widget"	: `push_button,
	"label"		: Label::DeleteButton (),
	"no_help"	: true,
	"handle"	: HandleDeleteUser,
    ],
    // ------------------
    ];
  }

  /**
   * Main dialog for Kiwi image configuration
   */
  define symbol KiwiDialog () {

    InitImageConfiguration ();

    map<string, map> widget_descr	= get_widget_description ();
    widget_descr["tab"] = CWMTab::CreateWidget($[
        "tab_order"	: ["config.xml", "description", "users", "scripts", "directories"],
	"tabs"		: tabs_descr (),
	"widget_descr"	: widget_descr,
	"initial_tab"	: "config.xml",
    ]);
    Wizard::SetContentsButtons("", `VBox (), "", Label::BackButton(), Label::NextButton());

    term contents	= `VBox (
	`Left (`Label (KiwiConfig["name"]:"")),
	"tab",
	`VSpacing (0.3),
	"global"
    );

    map caption		= $[
	// dialog caption
	"iso"	: _("Live CD Configuration"),
	// dialog caption
	"xen"	: _("Xen Image Configuration"),
	// dialog caption
	"usb"	: _("USB Stick Image Configuration"),
	// button label
	"vmx"	: _("Virtual Disk Image"),
    ];
    map next_button	= $[
	// button label
	"iso"	: _("&Create ISO"),
	// button label
	"xen"	: _("&Create Xen Image"),
	// button label
	"usb"	: _("&Create USB Stick Image"),
	// button label
	"vmx"	: _("&Create Virtual Disk Image"),
    ];
    symbol ret = CWM::ShowAndRun ($[
	"widget_names"		: [ "global", "tab" ],
	"widget_descr"		: widget_descr,
	"contents"		: contents,
	"caption"		: caption[kiwi_task]:"",
	"back_button"		: Label::BackButton (),
//	"next_button"		: next_button[kiwi_task]:Label::NextButton (),
	"next_button"		: Label::FinishButton (),
    ]);
    y2milestone("Returning %1", ret);
    return ret;
  }

  /**
   * Prepare dialog: define kiwi data without cd-creator
   */
  define symbol PrepareDialog () {

    if (kiwi_task == "")
	kiwi_task	= "iso";

    map<string,any> Config	= KiwiConfig;
    string kiwi_configuration	= Config["kiwi_configuration_" + kiwi_task]:"";
    string name			= Config["name"]:"";
    string out_dir		= Config["iso-directory"]:"";
    map <integer,string> repositories	= $[];
    // originally enabled list of sources
    list<map> enabled_sources	= [];

    // help text for kiwi UI preparation
    string help	= _("<p>Enter the name of your image configuration or <b>Import</b> the directory with the existing one.</p>") +

    // help text for kiwi UI preparation, cont.
    _("<p>Choose the <b>Image Type</b> which should be created.</p>") +

    // help text for kiwi UI preparation, cont.
    _("<p>Select <b>Output Directory</b> for the created image.</p>") +

    // help text for kiwi UI preparation, cont.
    _("<p>Modify the list of <b>Package Repositories</b> that will be used for creating the image. If the list is empty, current system repositories will be used.</p>") +

    // help text for kiwi UI preparation, cont.
    _("<p>Use <b>Next</b> to continue with the configuration.</p>");

    // on start, show the default YaST sources
    if (Config == $[])
    {
	Pkg::SourceRestore ();
	Pkg::SourceLoad();
	Config["sources"]	= maplist (map source, Pkg::SourceEditGet (), {
	    integer srcid       = source["SrcId"]:-1;
	    map data = Pkg::SourceGeneralData (srcid);
	    string url	= data["url"]:"";
	    repositories[srcid]	= url;
	    return url;
	});
    }
    else if (Config["sources"]:[] != [])
    {
	list sources	= Config["sources"]:[];
	list<map> all_sources = Pkg::SourceEditGet();
	foreach (map source, all_sources, {
	    integer srcid       = source["SrcId"]:-1;
	    map data = Pkg::SourceGeneralData (srcid);
	    string url	= data["url"]:"";
	    string stripped	= url;
	    if (substring (url, 0, 6) == "dir://")
		stripped	= substring (url, 6);
	    if (contains(sources, url) || contains (sources, stripped))
		repositories[srcid]	= url;
	});
    }


    // read the new configuration and update UI accordingly
    void update_config (string dir) {

	// 1. delete previous sources (were in repositories list)
	foreach (map source, Pkg::SourceEditGet (), {
	    integer srcid       = source["SrcId"]:-1;
	    if (haskey (repositories, srcid))
		Pkg::SourceDelete (srcid);
	});

	kiwi_configuration	= dir;
	Config	= eval (Kiwi::ReadConfigXML (kiwi_configuration));
	name	= Config["name"]:"";
	if (Config["iso-directory"]:"" != "")
	    UI::ChangeWidget (`id (`out_dir),`Value,Config["iso-directory"]:"");
	repositories	= $[];
	foreach (map repo, (list<map>) Config["repository"]:[], {
	    string source	= repo["source",0,"path"]:"";
	    if (source != "")
	    {
		integer id	= -1;
		string url	= "";
		if (substring (source, 0, 1) == "/")
		    url	= "dir://";
		if (repo["type"]:"" == "rpm-dir")
		{
		    id = Pkg::SourceCreateType (url + source, "", "Plaindir");
		}
		else
		    id = Pkg::SourceCreate (url + source, "");
		if (id != -1)
		    repositories[id]	= url + source;
	    }
	});
	UI::ChangeWidget (`id (`config), `Value, name);
	UI::ChangeWidget (`id (`repositories), `Items,
	    maplist (integer id, string repo, repositories, ``(
		`item (`id (id), repo)))
	);
	foreach (string type, map data, Config["type"]:$[], {
	    kiwi_task	= type;
	    if (data["primary"]:"" == "true")
		break;
	});
	UI::ChangeWidget (`id (`type), `Value, kiwi_task);
    }

    void update_repo_table () {
	UI::ChangeWidget (`id (`repositories), `Items, maplist (
	    integer id, string repo, repositories, ``(`item (`id (id), repo))
	));
    }

    term contents	= `VBox (
	`HBox (
	    // text entry label
	    `TextEntry (`id (`config), _("&Kiwi configuration"), name),
	    // push button label
	    `VBox (`Label (""), `PushButton (`id (`import), _("&Import")))
	),
	// frame label
	`Frame (_("Image Type"),
	  `RadioButtonGroup (`id (`type), `HBox (`HSpacing (), `VBox (
	    `VSpacing (0.2),
	    `Left (`RadioButton (`id ("iso"), `opt (`notify),
		// radio button label
		_("&Live ISO Image"), kiwi_task == "iso")),
	    `Left (`RadioButton (`id ("xen"), `opt (`notify),
		// radio button label
		_("&Xen Image"), kiwi_task == "xen")),
	    `Left (`RadioButton (`id ("usb"), `opt (`notify),
		// radio button label
		_("&USB Stick Image"), kiwi_task == "usb")),
	    // radio button label
	    `Left (`RadioButton (`id ("vmx"), `opt (`notify),
		_("&Virtual Disk Image"), kiwi_task == "vmx")),
	    `VSpacing (0.2)
	  )))
	),
	`HBox (
	    // text entry label
	    `TextEntry (`id (`out_dir), _("&Output directory"), out_dir),
	    `VBox (
		`Label (""),
		`PushButton (`id (`browse), Label::BrowseButton ())
	    )
	),
	`VSpacing (0.2),
	`Table (`id(`repositories), `header (
	    // table header
	    _("Package Repository"))
	),
	`HBox (
	    `PushButton (`id (`add), Label::AddButton ()),
	    `PushButton (`id (`edit), Label::EditButton ()),
	    `PushButton (`id (`delete), Label::DeleteButton ()),
	    `HStretch ()
	)
    );
    // dialog caption
    Wizard::SetContentsButtons (_("Image preparation"), contents, help,
	Label::BackButton(), Label::NextButton());

    if (Config == $[] && kiwi_configuration != "")
    {
	update_config (kiwi_configuration);
    }
    else
	update_repo_table ();

    UI::SetFocus (`id (`config));
    any ret = nil;
    while (true)
    {
	ret = UI::UserInput();
	if (ret == `abort || ret == `cancel || ret == `back)
	{
	    break;
	}
	else if (ret == `import)
	{
	    string dir	= UI::AskForExistingDirectory (
		kiwi_configuration== "" ? Kiwi::images_dir : kiwi_configuration,
		// popup for file selection dialog
		_("Directory to Import"));
	    if (dir!= nil)
	    {
		update_config (dir);
	    }
	}
	else if (ret == `add)
	{
	    Wizard::CreateDialog ();
	    string url	= "";
	    if (SourceDialogs::TypeDialog () == `next &&
		SourceDialogs::EditDialog () == `next)
	    {
		url = SourceDialogs::GetURL ();
	    }
	    Wizard::CloseDialog ();
	    if (url != "")
	    {
		boolean present	= false;
		foreach (integer id, string repo, repositories, {
		    if (repo == url)
			present	= true;
		});
		if (present) break;

		integer id	= -1;
		map parsed = URL::Parse(url);
		if (parsed["scheme"]:"" == "pkg")
		{
		    parsed["scheme"] = "dir";
		    url = URL::Build (parsed);
		    id = Pkg::SourceCreateType (url, "", "Plaindir");
		}
		else
		    id = Pkg::SourceCreate (url, "");

		if (id != -1)
		{
		    repositories[id]	= url;
		    update_repo_table ();
		}
	    }
	}
	else if (ret == `edit)
	{
	    integer selected	= (integer)
		UI::QueryWidget (`id (`repositories), `CurrentItem);
            map generalData	= Pkg::SourceGeneralData (selected);
            string url		= generalData["url"]:"";
	    string url_selected	= url;
	    boolean plaindir	= generalData["type"]:"YaST" == "Plaindir";
	    // change schema if the source type is plaindir
	    // to show the right popup dialog
	    if (plaindir)
	    {
		map parsed = URL::Parse(url);
		parsed["scheme"] = "pkg";
		url = URL::Build (parsed);
	    }
	    url	= SourceDialogs::EditPopup (url);
	    if (url == "" || url == nil || url == generalData["url"]:"")
		continue;
	    // remove current url + add new one
	    Pkg::SourceDelete (selected);
	    repositories	= remove (repositories, selected);
	    integer id	= -1;
	    map parsed = URL::Parse(url);
	    if (parsed["scheme"]:"" == "pkg")
	    {
		parsed["scheme"] = "dir";
		url = URL::Build (parsed);
		id = Pkg::SourceCreateType (url, "", "Plaindir");
	    }
	    else
		id = Pkg::SourceCreate (url, "");
	    repositories[id]	= url;
	    update_repo_table ();
	}
	else if (ret == `delete)
	{
	    integer selected	= (integer)
		UI::QueryWidget (`id (`repositories), `CurrentItem);
	    Pkg::SourceDelete (selected);
	    repositories	= remove (repositories, selected);
	    update_repo_table ();
	}
	else if (ret == `browse)
	{
	    string dir	= UI::AskForExistingDirectory ("",
		// popup for file selection dialog
		_("Path to the Output Directory"));
	    if (dir!= nil)
	    {
		UI::ChangeWidget (`id (`out_dir), `Value, dir);
	    }
	}
	else if (ret == `next)
	{
	    name	= (string) UI::QueryWidget (`id (`config), `Value);
	    if (name == "")
	    {
		// error popup
		Report::Error(_("Enter the name of the configuration."));
		UI::SetFocus (`id (`config));
		continue;
	    }
	    out_dir	= (string) UI::QueryWidget (`id (`out_dir), `Value);
	    if (out_dir == "")
	    {
		// error popup
		Popup::Error (_("Enter the path to the output directory."));
		UI::SetFocus (`id (`out_dir));
		continue;
	    }
	    kiwi_task	= (string)  UI::QueryWidget (`id (`type), `Value);
	    break;
	}
    }
    string boot	= Config["type",kiwi_task,"boot"]:"";
    if (boot != "" && issubstring (boot, "/"))
    {
	Config[kiwi_task + "boot"] = substring (boot, search (boot, "/") + 1);
    }
    if (ret == `next)
    {
	Config["name"]		= name;
	Config["kiwi_configuration_" + kiwi_task]	= kiwi_configuration;
        Config["_imported"]	= true;
        Config["iso-directory"]	= out_dir;
	Config["sources"]	= maplist (
	    integer id, string repo, repositories, ``(repo));
	if (Config["sources"]:[] != [])
	    ProductCreator::enable_sources	= false;
	else
	{
	    // reenable original sources
	    Pkg::SourceRestore ();
	    Pkg::SourceLoad();
	}
	ProductCreator::Config		= Config;
	ProductCreator::kiwi_task	= kiwi_task;
    }
    y2milestone("Returning %1", ret);
    return (symbol) ret;
  }
}
